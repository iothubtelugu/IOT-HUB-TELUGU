#include <Keypad.h>
#include <LiquidCrystal_I2C.h>
#include <Servo.h>

Servo myServo;
LiquidCrystal_I2C lcd(0x27, 16, 2);

// Password
String password = "1234";
String input = "";

// Keypad setup
const byte rows = 4;
const byte cols = 4;

char keys[rows][cols] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};

byte rowPins[rows] = {2, 3, 4, 5};
byte colPins[cols] = {6, 7, 8, 9};

Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, rows, cols);

void setup() {
  lcd.init();
  lcd.backlight();

  myServo.attach(10);
  myServo.write(0); // Lock position

  lcd.setCursor(0,0);
  lcd.print("SMART LOCK");
  delay(2000);
  lcd.clear();
}

void loop() {
  lcd.setCursor(0,0);
  lcd.print("Enter Password");

  char key = keypad.getKey();

  if (key) {
    if (key == '#') {
      lcd.clear();

      if (input == password) {
        lcd.print("Access Granted");
        myServo.write(180); // Unlock
        delay(3000);
        myServo.write(0);  // Lock
      } else {
        lcd.print("Access Denied");
        delay(2000);
      }

      input = "";
      lcd.clear();
    }
    else if (key == '*') {
      input = "";
      lcd.clear();
    }
    else {
      input += key;
      lcd.setCursor(input.length() - 1, 1);
      lcd.print("*");
    }
  }
}
